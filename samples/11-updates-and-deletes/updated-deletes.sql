SELECT * FROM sys.[external_data_sources]
SELECT * FROM sys.[database_scoped_credentials]
GO

DROP TABLE IF EXISTS [test].[ORDERS];
CREATE TABLE [test].[ORDERS]
(
	[O_ORDERKEY] [INT] NOT NULL,
	[O_CUSTKEY] [INT] NOT NULL,
	[O_ORDERSTATUS] [CHAR](1) NOT NULL,
	[O_TOTALPRICE] [DECIMAL](15, 2) NOT NULL,
	[O_ORDERDATE] [DATE] NOT NULL,
	[O_ORDERPRIORITY] [CHAR](15) NOT NULL,
	[O_CLERK] [CHAR](15) NOT NULL,
	[O_SHIPPRIORITY] [INT] NOT NULL,
	[O_COMMENT] [VARCHAR](79) NOT NULL,
	[O_ORDERYEAR]  AS (DATEPART(YEAR,[O_ORDERDATE])) PERSISTED,
	[O_ORDERMONTH]  AS (DATEPART(MONTH,[O_ORDERDATE]))
)

DROP EXTERNAL DATA SOURCE [Azure-Storage];
DROP DATABASE SCOPED CREDENTIAL [Storage-Credentials];
GO

CREATE DATABASE SCOPED CREDENTIAL [Storage-Credentials]
WITH IDENTITY = 'SHARED ACCESS SIGNATURE',
SECRET = ''; -- Remove starting question mark
GO

CREATE EXTERNAL DATA SOURCE [Azure-Storage]
WITH 
( 
	TYPE = BLOB_STORAGE,
 	LOCATION = 'https://myaccount.blob.core.windows.net',
 	CREDENTIAL= [Storage-Credentials]
);

TRUNCATE TABLE [test].[ORDERS]
GO

CREATE CLUSTERED INDEX IXC ON [test].[ORDERS] (O_ORDERKEY)
GO
CREATE INDEX ix1 ON [test].[ORDERS] (O_CUSTKEY)
CREATE INDEX ix2 ON [test].[ORDERS] (O_CLERK)
GO

BULK INSERT [test].[ORDERS]
FROM 'tpch/10GB/orders.tbl'
WITH
(
	DATA_SOURCE = 'Azure-Storage',
	FIELDTERMINATOR = '|',
	BATCHSIZE=10000
)
GO

SELECT O_CLERK, COUNT(*) FROM [test].[ORDERS]  GROUP BY O_CLERK
GO

DELETE FROM [test].[ORDERS] 
OUTPUT deleted.*
WHERE O_CLERK = 'Clerk#000000001'
GO

UPDATE [test].[ORDERS] 
OUTPUT deleted.*, inserted.*
SET O_COMMENT = 'Comment Here'
WHERE O_CLERK LIKE 'Clerk#000000002'

SELECT COUNT(*) FROM [test].[ORDERS] 
WHERE O_CLERK LIKE 'Clerk#0000006__'
GO

SET NOCOUNT ON
WHILE (1=1)
BEGIN
	DELETE TOP(1000) FROM [test].[ORDERS]  WHERE O_CLERK LIKE 'Clerk#0000006__'
	IF (@@ROWCOUNT = 0) BREAK
END
